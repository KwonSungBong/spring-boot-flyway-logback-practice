apply plugin: 'org.flywaydb.flyway'

def migrationDb = System.properties['migration.db']
def targetVersion = System.properties['target.version']

if (migrationDb != null) {
    println "migration db - $migrationDb, target version - $targetVersion"

    flyway {
        user = 'user'
        password = 'password'

        if (migrationDb == 'local') {
            url DB_URL_LOCAL
        } else if (migrationDb == 'dev') {
            url DB_URL_DEV
        }

        if( targetVersion != null){
            target = targetVersion
        }

        locations = ['filesystem:src/main/flyway']
    }
}

/*
 * flywayClean의 경우 해당 데이터베이스의 모든 테이블이 제거된다.
 * 해당 명령 수행 시 운영 환경에서는 심각한 문제를 일으킬 수 있으므로
 * overwrite 를 통해 local환경일 때만 수행하게 한다.
 * 하지만 해당 태스크의 수행이 필요한 경우는 주석으로 막고 수행하도록 한다.
 */
flywayClean {
    if (project.gradle.startParameter.taskNames.contains('flywayClean') && migrationDb != 'local') {
        throw new RuntimeException('This ENV. does not support flywayClean.')
    }
}